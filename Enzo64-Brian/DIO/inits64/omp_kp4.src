!=======================================================================
!//////////////////////  SUBROUTINE MAKE_FIELD  \\\\\\\\\\\\\\\\\\\\\\\\

      subroutine make_field_kpreserving(field, nx, ny, nz, 
     &                      in, jn, kn, itype, iseed, box,
     &                      PSTable, PSMin, PSStep, kfcutoff)

!  COMPUTES RANDOM GAUSSIAN FIELD FROM SPECIFIED POWER SPECTRUM
!
!  written by: Greg Bryan
!  date:       June, 1997
!  modified:   Robert Harkness
!  date:       November, 2003
!
!  PURPOSE: 
!
!  INPUTS:
!        i,j,kn      = real dimensions of green
!        nx,ny,nz    = active dimensions of green
!        itype       = field type (0 - density, 1/2/3 - x/y/z displacement)
!        iseed       = random number seed (negative)
!        box         = size
!        PSTable     = Table of precomputed PS values
!        PSMin       = minimum x value in PSTable
!        PSStep      = x step in PSTable
!        kfcutoff    = high k filter (sharp) in units of the fundamental
!
!  Outputs:
!        field       = gaussian random field
!
!  LOCALS:
!        num_dim     = number of dimensions to be used for force law
!        nx,y,zmid   = midpoint (+1) of the grid in each axis
!        nx,y,zd2    = number of grid points divided by 2 for each axis

      implicit NONE

!     Arguments

      integer :: in, jn, kn, nx, ny, nz, nxmax, nymax, nzmax, 
     &           itype, iseed, kfcutoff
      real ::    field(in, jn, kn), box, 
     &           PSMin, PSPart, PSStep, PSTable(1)

!     Locals

      integer :: i, i1, j, j1, n, n1, l, m
      real ::    dummy, twopi, kcutoffsq, dk
      complex :: z

      integer :: long_seed

      real :: rr(2)
      real*8 :: t1, t2,wall_clock

      real, allocatable :: ranbuf(:,:,:,:,:)

      integer :: first_pass
      integer :: lrec

      data first_pass / 0 /

      save first_pass
      save ranbuf

!     External functions

      real ::    ran1
      real ::    enzo_ranf

!     Set constants

      twopi  = 8.0*atan(1.0)
      dk     = twopi/box
      kcutoffsq = 1.0e30
      if (kfcutoff .gt. 0) kcutoffsq = (kfcutoff*dk)**2

!     Initialize random # generator with random seed

      long_seed = iseed
      n = iseed
!     dummy = ran1(n)
      call enzo_seed(long_seed)
!     Loop over k-box sizes, so that we fill k-space from low-k outwards

      if ( first_pass == 0 ) then

      allocate( ranbuf(1:2, 1:5, -nx/2 : nx/2, -nx/2 : nx/2, 1 : nx/2) )

!     if ( first_pass == 0 ) then
!     lrec = 8 * (2 * 5 * (2*(nx/2)+1) * (2*(nx/2)+1) )
!     write(0,'("Open direct access file with recl = ",i8," nx = ",i4)')
!    &      lrec, nx
!     open(120, file='random_numbers', form='unformatted',
!    &          status='unknown', access='direct',
!    &          recl = 8 * (2 * 5 * (2*(nx/2)+1) * (2*(nx/2)+1) ) )
!     end if

      do n=1,nx/2

         do i=-n+1, n

            do j=-n+1, n
               ranbuf(1,1,j,i,n) = enzo_ranf()
               ranbuf(2,1,j,i,n) = enzo_ranf()
               if (i .ge. 0 .and. i .ne. n) then
                 ranbuf(1,2,j,i,n) = enzo_ranf()
                 ranbuf(2,2,j,i,n) = enzo_ranf()
                 ranbuf(1,3,j,i,n) = enzo_ranf()
                 ranbuf(2,3,j,i,n) = enzo_ranf()
               else
                 ranbuf(1,2,j,i,n) = 0.0
                 ranbuf(2,2,j,i,n) = 0.0
                 ranbuf(1,3,j,i,n) = 0.0
                 ranbuf(2,3,j,i,n) = 0.0
               end if
               if (i .ge. 0 .and. i .ne. n .and.
     &             j .ne. -n+1 .and. j .ne. n) then
                 ranbuf(1,4,j,i,n) = enzo_ranf()
                 ranbuf(2,4,j,i,n) = enzo_ranf()
                 ranbuf(1,5,j,i,n) = enzo_ranf()
                 ranbuf(2,5,j,i,n) = enzo_ranf()
               else
                 ranbuf(1,4,j,i,n) = 0.0
                 ranbuf(2,4,j,i,n) = 0.0
                 ranbuf(1,5,j,i,n) = 0.0
                 ranbuf(2,5,j,i,n) = 0.0
               end if
            end do
         end do
      end do

!     if ( first_pass == 0 ) then

!     do n = 1,nx/2
!       write(120, rec=n)
!    &  ((((ranbuf(l,m,j,i,n),l=1,2),m=1,5),j=-nx/2,nx/2),i=-nx/2,nx/2)
!     end do

!     close(120)

      first_pass = -1

      end if

      t1 = wall_clock()

!$omp parallel
!$omp-  shared(dk, PSMin, PSStep, PSTable, itype, kcutoffsq, box, nx)
!$omp-  shared(ranbuf, field)
!$omp-  private(i, j, n, i1, j1, n1, rr, z)
!$omp-  default(none)
!$omp do
      do n=1, nx/2

         do i=-n+1, n

            do j=-n+1, n

               i1 = mod(i+nx,nx)+1
               j1 = mod(j+nx,nx)+1
               n1 = mod(1-n+nx,nx)+1

!              1) +i plane

               rr(1) = ranbuf(1,1,j,i,n)
               rr(2) = ranbuf(2,1,j,i,n)
               call processk(n,i,j, dk, PSMin, PSStep, PSTable, 
     &                       itype, z, kcutoffsq, box, rr)

               field((n+1)*2-1,i1,j1) = real(z)
               field((n+1)*2  ,i1,j1) = imag(z)

!              2) +j and -j plane
!                 (the i .ne. n is to avoid overlapping with (1))

               if (i .ge. 0 .and. i .ne. n) then

                  rr(1) = ranbuf(1,2,j,i,n)
                  rr(2) = ranbuf(2,2,j,i,n)
                  call processk(i,n,j, dk, PSMin, PSStep, PSTable, 
     &                          itype, z, kcutoffsq, box, rr)

                  field(i1*2-1,n+1,j1) = real(z)
                  field(i1*2  ,n+1,j1) = imag(z)

                  rr(1) = ranbuf(1,3,j,i,n)
                  rr(2) = ranbuf(2,3,j,i,n)
                  call processk(i,1-n,j, dk, PSMin, PSStep, PSTable,
     &                          itype, z, kcutoffsq, box, rr)

                  field(i1*2-1,n1,j1) = real(z)
                  field(i1*2  ,n1,j1) = imag(z)

               endif

!              3) +k and -k plane
!                 (the logic involving j is to avoid overlapping with (2))

               if (i .ge. 0 .and. i .ne. n .and. 
     &             j .ne. -n+1 .and. j .ne. n) then

                  rr(1) = ranbuf(1,4,j,i,n)
                  rr(2) = ranbuf(2,4,j,i,n)
                  call processk(i,j,n, dk, PSMin, PSStep, PSTable,
     &                          itype, z, kcutoffsq, box, rr)

                  field(i1*2-1,j1,n+1) = real(z)
                  field(i1*2  ,j1,n+1) = imag(z)

                  rr(1) = ranbuf(1,5,j,i,n)
                  rr(2) = ranbuf(2,5,j,i,n)
                  call processk(i,j,1-n, dk, PSMin, PSStep, PSTable,
     &                          itype, z, kcutoffsq, box, rr)

                  field(i1*2-1,j1,n1) = real(z)
                  field(i1*2  ,j1,n1) = imag(z)

               endif

            enddo
         enddo
      enddo
!$omp end do
!$omp end parallel

!     deallocate( ranbuf )

      t2 = wall_clock()

      write(0, '("dt ", 1pe16.9)') t2-t1

!$omp parallel
!$omp-  shared(field, in, jn, kn, nx, ny, nz)
!$omp-  private(i, j, n)
!$omp-  default(none)
!$omp do
      do n=1, kn
         do j=1, jn
            do i=1, in
               field(i,j,n) = field(i,j,n) * real(nx*ny*nz)
            enddo
         enddo
      enddo
!$omp end do
!$omp end parallel

!     Clear the zero wavenumber position

      field(1,1,1) = 0.0
      field(2,1,1) = 0.0

!     Adjust the field to satisfy the conjugate relations that
!     are implied by a zero imaginary part.

      call adjfft(field, nx, ny, nz, in, jn)

      return
      end


c===================================================================

      subroutine processk(i, j, k, dk, PSMin, PSStep, PSTable, 
     &                    itype, z, kcutoffsq, box, rr)

      implicit none

!     Parameter

      real, parameter :: twopi = 2.0*3.14159265358979324

!     Arguments

      integer :: i, j, k, itype
      real ::    dk, PSMin, PSStep, PSTable(*)
      real ::    kcutoffsq, box
      real ::    rr(2)
      complex :: z

!     Locals

      integer :: index
      real :: psval, kdir, klog, ang, amp, kmodsq
      real :: ranf_min

!     External function

      real :: ran1
      real :: enzo_ranf



!     Define table lookup function
 
      real ::    Table1, Table2, Step, Min, Tablex, TableLookUp
      integer :: Tablei

      TableLookUp(Table1, Table2, Step, Min, Tablei, Tablex) = 
     &            Table1 + (Tablex - real(Tablei-1)*Step - Min) 
     &            / Step * (Table2 - Table1)


      kmodsq = max(i**2 + j**2 + k**2, 1)*dk**2
      klog   = 0.5*log(kmodsq)
      index = int((klog - PSMin)/PSStep)
      psval = TableLookUp(PSTable(index), PSTable(index+1),
     &                    PSStep, PSMin, index, klog)
      psval = psval * dk**3

      if (kmodsq .gt. kcutoffsq) psval = 0.0

!     Generate a complex number with random phase and amplitude
!     Gaussian distributed with a mean of sqrt(psval) with the
!     Box-Muller method.  Note we have supressed a factor of
!     sqrt(2) since we must also divide by this factor to account
!     for the dreary fact that we are really generating two random
!     fields (if we were doing a complex-to-complex transform
!     this would show up when we discarded the perfectly
!     good imaginary component of the transformed field).  whew.

      ranf_min = 1.0e-37

      ang = twopi*rr(1)
      amp = sqrt(-log(max(rr(2),ranf_min)) * psval)
      z   = cmplx(cos(ang), sin(ang)) * amp

!     Process this on the basis of itype:
!      0)   density field - just leave it be.
!      1-3) displacement field - multiply by vec(k)/k^2
!           (and then convert from Mpc to fraction of box).

      if (itype .ne. 0) then
         if (itype .eq. 1) kdir = real(i)*dk
         if (itype .eq. 2) kdir = real(j)*dk
         if (itype .eq. 3) kdir = real(k)*dk
         z = z * cmplx(0.0,1.0) * kdir / (kmodsq * box)
      endif

      return
      end
