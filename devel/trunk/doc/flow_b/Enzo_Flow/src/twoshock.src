#include "fortran.def"
c=======================================================================
c///////////////////////  SUBROUTINE TWOSHOCK  \\\\\\\\\\\\\\\\\\\\\\\\\
c
      subroutine twoshock(
     &           dls, drs, pls, prs, uls, urs, 
     &           idim, jdim, i1, i2, j1, j2, dt, gamma, pmin, ipresfree,
     &           pbar, ubar, gravity, grslice, idual, eta1
     &                   )
c
c  RIEMANN SOLVER USING TWO-SHOCK APPROXIMATION
c
c  written by: Jim Stone
c  date:       January, 1991
c  modified1:  July, 1994 by Greg Bryan; made slicewise version
c  modified2:  March, 1995 by Greg Bryan; added gravity
c  modified3:  May, 2003 by Alexei Kritsuk; fixed sign in initial guess
c
c  PURPOSE:
c     This routine performs an approximate Riemann solution for each
c     zone edge described by dls, drs (etc) where dls(i) is the density
c     on the left side of zone interface (i), and drs(i) is the density
c     on the right side of zone interface (i).  Note that this differs
c     from the zone-centered way of indexing things.
c
c  INPUT:
c    dt     - timestep in problem time
c    eta1   - (dual) selection parameter for total energy (typically ~0.001)
c    gamma  - ideal gas law constant
c    gravity - gravity flag (0 = off, 1 = on)
c    grslice - acceleration in this direction in this slice
c    i1,i2  - starting and ending addresses for dimension 1
c    idim   - declared leading dimension of slices
c    idual  - dual energy formalism flag (0 = off)
c    ipresfree - pressure free flag (0 = off, 1 = on, i.e. p=0)
c    j1,j2  - starting and ending addresses for dimension 2
c    jdim   - declared second dimension of slices
c    dl,rs  - density at left and right edges of each cell
c    pl,rs  - pressure at left and right edges of each cell
c    pmin   - minimum allowed pressure
c    ul,rs  - 1-velocity at left and right edges of each cell
c    
c  OUTPUT:
c    pbar   - the pressure at the (left) cell interface 
c             after applying the Riemann solver
c    ubar   - the (1,2,3) velocity at the (left) cell interface
c             after applying the Riemann solver
c    
c  EXTERNALS:
c
c  LOCALS:
c
c  PARAMETERS:
c    numiter - number of Newton iterations to perform [3]
c
c-----------------------------------------------------------------------
      implicit NONE
c
      integer ijkn
      parameter (ijkn=MAX_ANY_SINGLE_DIRECTION)
c-----------------------------------------------------------------------
c
c  argument declarations
c
      integer gravity, i1, i2, idim, idual, ipresfree, j1, j2, jdim
      real    dt, eta1, gamma, pmin
      real      dls(idim,jdim),     drs(idim,jdim),    pbar(idim,jdim),
     &          pls(idim,jdim),     prs(idim,jdim),    ubar(idim,jdim),
     &          uls(idim,jdim),     urs(idim,jdim), grslice(idim,jdim)
c
c  local declarations
c
      integer i, j, n
      real     cl(ijkn),    cr(ijkn), dpdul(ijkn), dpdur(ijkn),
     &         ps(ijkn),    ubl(ijkn),   ubr(ijkn),
     &         qa      ,     zl(ijkn),    zr(ijkn)
c
c  parameters
c
      integer numiter
      parameter( numiter = 8 )
c
c\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\//////////////////////////////////
c=======================================================================
c
c     write(6,*) 'TWOSHOCK: dt =',dt,'  idim =',idim,'  jdim=',jdim
c     write(6,*) 'TWOSHOCK: i1 =',i1,'  i2   =',i2
c     write(6,*) 'TWOSHOCK: j1 =',j1,'  j2   =',j2
c
      qa = (gamma + 1.0)/(2.0*gamma)
c
c  Loop through this slice, doing 1 sweep at a time
c
      do j=j1,j2
c
c  If pressfree conditions are needed, set pbar to zero and ubar to the
c     average of left and right velocity states.
c
       if (ipresfree .eq. 1) then
        do i=i1, i2
           pbar(i,j) = pmin
           ubar(i,j) = 0.5*(uls(i,j)+urs(i,j))
           pls(i,j)  = pmin
           prs(i,j)  = pmin
        enddo
c
c  Otherwise, solve Riemann problem
c
       else
c
c  First guess at pbar and left- and right-ubar
c     (or is it a plus? :    &     + cr(i)*cl(i)*(uls(i,j)-urs(i,j))
c  kritsuk: it is a plus: see van Leer 1979, JCP 32, 101 (eq. 60 on page 109).
c
c       if (gravity .eq. 1) then
       if (gravity .eq. 99) then
        do i=i1,i2
          cl  (i) = sqrt(gamma*pls(i,j)*dls(i,j))
          cr  (i) = sqrt(gamma*prs(i,j)*drs(i,j))
          ps(i) = (cr(i)*pls(i,j) + cl(i)*prs(i,j)
     &          +  cr(i)*cl(i)*(uls(i,j) - urs(i,j)
     &                        + 0.5*dt*(grslice(i-1,j)-grslice(i,j)) 
     &                                           ) )/(cr(i)+cl(i))
          if (ps(i) .lt. pmin) ps(i) = pmin
        enddo
       else
        do i=i1,i2
          cl  (i) = sqrt(gamma*pls(i,j)*dls(i,j))
          cr  (i) = sqrt(gamma*prs(i,j)*drs(i,j))
          ps(i) = (cr(i)*pls(i,j) + cl(i)*prs(i,j)
     &          +  cr(i)*cl(i)*(uls(i,j) - urs(i,j) 
     &                                           ) )/(cr(i)+cl(i))
            if (ps(i) .lt. pmin) ps(i) = pmin
c          if (ps(i) .lt. pmin) ps(i) = min(pls(i,j),prs(i,j))
c          if (ps(i) .lt. pls(i,j) .and. ps(i) .lt. prs(i,j))
c     &         ps(i) = min(pls(i,j), prs(i,j))
        enddo
       endif
c
c  Newton iterations to compute succesive guesses for pbar, l and r ubar
c    (there are some gravity changes in here but they have not really
c     been worked out yet).
c
        do n=2, numiter
          do i=i1,i2
            zl (i) = cl(i)*sqrt((1.0+qa*(ps(i)/pls(i,j)-1.0)))
            zr (i) = cr(i)*sqrt((1.0+qa*(ps(i)/prs(i,j)-1.0)))
            ubl(i) = uls(i,j) - (ps(i)-pls(i,j))/zl(i)
            ubr(i) = urs(i,j) + (ps(i)-prs(i,j))/zr(i)
          enddo
          do i=i1,i2
            dpdul(i) = -4.0*zl(i)**3/dls(i,j)
     &        /(4.0*zl(i)**2/dls(i,j) - (gamma+1.0)*(ps(i)-pls(i,j)))
            dpdur(i) =  4.0*zr(i)**3/drs(i,j)
     &        /(4.0*zr(i)**2/drs(i,j) - (gamma+1.0)*(ps(i)-prs(i,j)))
            ps (i) = ps(i) + (ubr(i)-ubl(i))*dpdur(i)*dpdul(i)
     &                      /(dpdur(i)-dpdul(i))
c            if (ps(i) .lt. pmin) ps(i) = min(pls(i,j),prs(i,j))
            if (ps(i) .lt. pmin) ps(i) = pmin
          enddo
        enddo
c
c  Compute final values of resolved state
c
        do i=i1,i2
          if (ps(i) .lt. pmin) ps(i) = min(pls(i,j),prs(i,j))
          pbar(i,j) = ps(i)
          ubar(i,j) = ubl(i) + (ubr(i)-ubl(i))*dpdur(i)/
     &                         (dpdur(i)-dpdul(i))
        enddo
c
c  Dual energy formalism: if sound speed squared from pbar is less than 
c     eta1*v^2 then discard pbar and use average of pls/prs.
c
#ifdef UNUSED
        if (idual .eq. 1) then
           do i=i1, i2
              if (gamma*ps(i)/min(dls(i,j),drs(i,j)) .lt.
     &             eta1*ubar(i,j)**2) then
                 pbar(i,j) = 0.5*(pls(i,j) + prs(i,j))
                 ubar(i,j) = 0.5*(uls(i,j) + urs(i,j))
              endif
           enddo
        endif
#endif /* UNUSED */
c
       endif
c
      enddo
c
      return
      end
